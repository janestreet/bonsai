---
title: "1: Constructors"
uuid: 05d4875b-921d-4ce5-aaf6-44c897f1720b
parent: "./api_tour.mdx"
---

<!--
```ocaml
open! Core_kernel
module Vdom = struct
  module Attr = struct
      type t
      let on_click _ :t = assert false
  end

  module Event = Ui_event

  module Node = struct
    type t
    let div (_ : Attr.t list) (_: t list) : t = assert false
    let button (_ : Attr.t list) (_: t list) : t = assert false
    let text (_: string) :t = assert false
  end
end
```
-->

## <tt>Bonsai.of\_module</tt>

`Bonsai.of_module` is the most powerful and common component constructor in the
API.  When a module implements the `Bonsai.S` interface, `Bonsai.of_module` can
be called on the first-class representation of that module.

```ocaml
module type S = sig
  (** The name of the component.  This is used to identify the component while
  debugging, and to annotate error messages.  If you can't think of a good
  name, a reasonable fallback is [Source_code_position.to_string [%here]]. *)
  val name : string

  (** A component receives read-only input, either as output from other
  components or from an external system (e.g. data from a server).  The input
  is frequently dynamic, but may also be constant. *)
  module Input : sig
    type t
  end

  (** A component's model is a state-machine that the component can read, but
  also write to.  Because both the input and model are readable, it can be hard
  to decide whether to request some data from the input or the model.  It is
  highly recommended to put just the data that needs mutation in [Model.t], and
  the rest in [Input.t]. *)
  module Model : sig
    type t [@@deriving sexp, equal]
  end

  (** Components can change their own [Model.t] by issuing "actions" that
  perform the state transition.  If you think of the state machine as
  having state-nodes of type [Model.t], then the arrows between those nodes
  would be of type [Action.t]. *)
  module Action : sig
    type t [@@deriving sexp_of]
  end

  (** While UI components stereotypically produce some kind of "view", with
  Bonsai, components are small and easy enough to compose that Bonsai
  components frequently produce intermediate results which are then wired into
  other components. *)
  module Result : sig
    type t
  end

  (** [compute] is a function from input and model to the component's result.
  In a component that produces a view, this function could be thought of as the
  "view computation function".

  This function is also given an "inject" function which converts this
  component's [Action.t] to a global [Vdom.Event.t] which can be given to
  Bonsai to schedule.  Frequently, this Vdom.Event.t is embedded within the
  result as a handler for some kind of user input. *)
  val compute : inject:(Action.t -> Vdom.Event.t) -> Input.t -> Model.t -> Result.t

  (** When an action is raised by this component (via an Vdom.Event.t), Bonsai
  will eventually pass that action back to that component's [apply_action]
  function.  This function is responsible for looking at the model and the
  incoming action and producing a new model.

  If [Model.t] represents a state-node in a state-machine, then apply_action is
  the implementation of transitions between states. *)
  val apply_action
    :  inject:(Action.t -> Vdom.Event.t)
    -> schedule_event:(Vdom.Event.t -> unit)
    -> Input.t
    -> Model.t
    -> Action.t
    -> Model.t
end
```

For more on the state-machine like nature of a bonsai component, read the
[Bonsai Concepts](./concepts.md) page.

```ocaml
# #show Bonsai.of_module
val of_module :
  ('input, 'model, 'action, 'result) Bonsai__Import.component_s ->
  default_model:'model -> ('input, 'result) Bonsai.t
```

When implementing `Bonsai.S` and `Bonsai.of_module`, it is common to
have a component take up an entire file, with the submodules and functions
declared as modules and functions at the top-most level of that file.


### Examples

The most useless module that implements `Bonsai.S`:

```ocaml
module Useless : sig
  (* This is typically a whole mli file *)

  include Bonsai.S
    with type Input.t  = unit
     and type Model.t  = unit
     and type Result.t = unit
end = struct
  (* This is typically a whole ml file *)

  module Input = Unit
  module Model = Unit
  module Action = Unit
  module Result = Unit

  let name = "useless"
  let compute ~inject:_ () () = ()
  let apply_action ~inject:_ ~schedule_event:_ () () () = ()
end
```

And then using that module to make a useless `Bonsai.t` component.

```ocaml
# let useless = Bonsai.of_module (module Useless) ~default_model:()
val useless : (unit, unit) Bonsai.t = <fun>
```

A basic component that tracks an integer in the state machine, and whose result
is both the view of that integer, and also some buttons that trigger actions
to increment or decrement the counter.

<!--
```ocaml env=e1
open! Core_kernel
module Vdom = struct
  module Attr = struct
      type t
      let on_click _ :t = assert false
  end

  module Event = Ui_event

  module Node = struct
    type t
    let div (_ : Attr.t list) (_: t list) : t = assert false
    let button (_ : Attr.t list) (_: t list) : t = assert false
    let text (_: string) :t = assert false
  end
end
```
-->

```ocaml env=e1
module Counter : sig
  (* This is typically a whole mli file *)

  include Bonsai.S
    with type Input.t = unit
     and type Model.t = int
     and type Result.t = Vdom.Node.t
end = struct
  (* This is typically a whole ml file *)

  module Input = Unit
  module Model = Int
  module Result = Vdom.Node

  let name = "Counter"

  module Action = struct
    type t =
      | Increment
      | Decrement
    [@@deriving sexp_of]
  end

  let apply_action ~inject:_ ~schedule_event:_ () model = function
    | Action.Increment -> model + 1
    | Action.Decrement -> model - 1

  let compute ~inject () model =
    let button label action =
      let on_click = Vdom.Attr.on_click (fun _ -> inject action) in
      Vdom.Node.button [ on_click ] [ Vdom.Node.text label ]
    in
    Vdom.Node.div
      []
      [ button "-1" Action.Decrement
      ; Vdom.Node.text (Int.to_string model)
      ; button "+1" Action.Increment
      ]
end
```

And then using that module to make a component.

```ocaml env=e1
# let counter = Bonsai.of_module (module Counter) ~default_model:0;;
val counter : (unit, Counter.Result.t) Bonsai.t = <fun>
```

<!-- Use the <tt> element to get monospace text without the bright-blue that
comes with `code blocks` -->
## <tt>Bonsai.const</tt>

`Bonsai.const` creates a Bonsai component with an unchanging output.  It does
not have an input or model. [^tick-a-model-and-input]  Compare to `Fn.const`.

```ocaml
# Bonsai.const
- : 'result -> ('a, 'result) Bonsai.t = <fun>
```

A constant component is not used frequently, but can be useful when an API that
you want to call requires a component.

### Examples

```ocaml
# let the_answer = Bonsai.const 42
val the_answer : ('_weak1, int) Bonsai.t = <fun>
```

## <tt>Bonsai.pure</tt>

`Bonsai.pure` is used to create a Bonsai component that can be implemented as a
pure function from `'input` to `'result`.

```ocaml
# #show Bonsai.pure
val pure : f:('input -> 'result) -> ('input, 'result) Bonsai.t
```

### Examples

A component whose input is an OCaml string, and whose result is a Virtual DOM text
node that will display the contents of that string.

```ocaml
# let string_to_vdom_text = Bonsai.pure ~f:(fun string -> Vdom.Node.text string);;
val string_to_vdom_text : (string, Vdom.Node.t) Bonsai.t = <fun>
```

A component whose input is a set of usernames, and whose result is a text
representation summarizing the set of people that like your twitter "content".

```ocaml
# let users_to_summary =
  Bonsai.pure ~f:(fun user_set ->
    let render_cutoff = 5 in
    match Set.length user_set with
    | 0 -> Vdom.Node.text "no one liked your post :("
    | n ->
      let first_5_people =
        user_set |> Set.to_list |> Fn.flip List.take render_cutoff |> String.concat ~sep:", "
      in
      if n <= render_cutoff
      then Vdom.Node.text (sprintf "%s liked your post" first_5_people)
      else (
        let others_count = n - render_cutoff in
        Vdom.Node.text
          (sprintf "%s and %d others liked your post" first_5_people others_count)))
val users_to_summary : ((string, '_weak2) Set.t, Vdom.Node.t) Bonsai.t =
  <fun>
```

## <tt>Bonsai.state\_machine</tt>

`Bonsai.state_machine` is a function that can be used to define a component
solely in terms of its "apply action" function.

```ocaml
# #show Bonsai.state_machine
val state_machine :
  (module Bonsai.Model with type t = 'model) ->
  (module Bonsai.Action with type t = 'action) ->
  Lexing.position ->
  default_model:'model ->
  apply_action:(inject:('action -> Ui_event.t) ->
                schedule_event:(Ui_event.t -> unit) ->
                'input -> 'model -> 'action -> 'model) ->
  ('input, 'model * ('action -> Ui_event.t)) Bonsai.t
```

With just apply-action being defined, the result value of the component is just
the inject function for the action.

### Example

This example shows how to define the counter-component from the `of_module` section,
but with `state_machine` instead.

```ocaml
module Action = struct
  type t =
    | Increment
    | Decrement
  [@@deriving sexp_of]
end

let component =
  let%map.Bonsai model, inject = Bonsai.state_machine
    (module Int)
    (module Action)
    [%here]
    ~default_model:0
    ~apply_action:(fun ~inject:_ ~schedule_event:_ () model -> function
      | Action.Increment -> model + 1
      | Action.Decrement -> model - 1) in
  let button label action =
    let on_click = Vdom.Attr.on_click (fun _ -> inject action) in
    Vdom.Node.button [ on_click ] [ Vdom.Node.text label ]
  in
  Vdom.Node.div
    []
    [ button "-1" Action.Decrement
    ; Vdom.Node.text (Int.to_string model)
    ; button "+1" Action.Increment
    ]
```

[^tick-a-input]: When the `'input` parameter in a Bonsai component is
  parametric, it signifies that the component does not read from the input, so
  the input can be anything.
