---
title: "3: Projections"
uuid: 98a0ce0e-6f1c-4b0b-8fcc-0e46f1e4e4e4
parent: "./api_tour.mdx"
---

<!--
```ocaml
open! Core_kernel
module Vdom = struct 
  module Attr = struct 
      type t 
      let on_click _ :t = assert false
  end
  module Node = struct 
    type t
    let div (_ : Attr.t list) (_: t list) : t = assert false 
    let button (_ : Attr.t list) (_: t list) : t = assert false 
    let text (_: string) :t = assert false
  end
end
```
-->

# Projections

The component combinators defined in section 2 have a somewhat confusing design
in that the input and models of the components must be the same in order for
them to be combined.  If every component in an app required the _entire app's
model_, this would be a terrible separation of concerns, not to mention that it
would be impossible to make general-purpose, reusable components.

A "Projection" is a function that takes a `Bonsai.t` and returns a `Bonsai.t`
with differing `'input` or `'result` types.  

<!--
```ocaml
let counter: (unit, Vdom.Node.t) Bonsai.t = Bonsai.pure ~f:(fun _ -> failwith "")
```
-->

## <tt> Bonsai.map </tt>

Like other applicatives, Bonsai defines a `map` function that returns a new
Bonsai component with the result type parameter transformed.


```ocaml
# #show Bonsai.map
val map : ('input, 'r1) Bonsai.t -> f:('r1 -> 'r2) -> ('input, 'r2) Bonsai.t
```

Also the infix operator:

```ocaml
# #show Bonsai.Infix.( >>| )
val ( >>| ) :
  ('input, 'r1) Bonsai.t -> ('r1 -> 'r2) -> ('input, 'r2) Bonsai.t
```

### Examples
```ocaml
# open Bonsai.Infix

# let int_returning = Bonsai.const 5;
val int_returning : ('_weak1, int) Bonsai.t = <fun>

# let string_returning = int_returning >>| Int.to_string
val string_returning : ('_weak2, string) Bonsai.t = <fun>
```

## <tt> Bonsai.map\_input </tt>

Making `'input` types line up is important for composition.

Because of how common this operation is (just like `>>|` and `map`), Bonsai
provides a function and infix operator for transforming the `'input` type
of a component:

```ocaml
# #show Bonsai.map_input
val map_input :
  ('i2, 'result) Bonsai.t -> f:('i1 -> 'i2) -> ('i1, 'result) Bonsai.t
```

There's also a right-associative operator that does the same thing.  Think of it
as a dual to `>>|`.

```ocaml
# #show Bonsai.Infix.( @>> )
val ( @>> ) :
  ('i1 -> 'i2) -> ('i2, 'result) Bonsai.t -> ('i1, 'result) Bonsai.t
```

`@>>` is pronounced "the drill operator", but I'm still partial to "the
infix-operator that will pierce the heavens".

### !Warning!

When using the drill operator, you may need more parentheses for grouping
than you may expect.

For example:

```
Input_from_server.response
@>> Model.Response.queries
@>> fun queries -> Queries.Input.create ~queries
@>> Queries.component
```

should actually be written as

```
Input_from_server.response
@>> Model.Response.queries
@>> (fun queries -> Queries.Input.create ~queries)
@>> Queries.component
```

### Examples

Given a component that stringifies ints, we can produce a component that
stringifies the lengths of lists with `@>>`.

```ocaml
# let int_to_string_component = Bonsai.pure ~f:Int.to_string
val int_to_string_component : (int, string) Bonsai.t = <fun>

# #show List.length
val length : 'a list -> int

# let list_to_len_string = List.length @>> int_to_string_component
val list_to_len_string : ('_weak3 list, string) Bonsai.t = <fun>
```

`@>>` can be used to project fields out of a record and build a
component that can operate on a more general type.

```ocaml
module Person = struct
  type t =
    { name: string
    ; age : int
    } [@@deriving fields]
end
```

```ocaml
# let print_person_age = Person.age @>> int_to_string_component
val print_person_age : (Person.t, string) Bonsai.t = <fun>
```

## <tt> Bonsai.enum </tt>

`Bonsai.enum` is how a Bonsai component can branch on its input or model
and provide a different Bonsai component that handles different cases.

```ocaml
# Bonsai.enum
- : (module Bonsai__.Import.Enum with type t = 'key) ->
    which:('input -> 'key) ->
    handle:('key -> ('input, 'result) Bonsai.t) -> ('input, 'result) Bonsai.t
= <fun>
```

The three required parameters for `Bonsai.enum` are

#### A first-class module satisfying <tt> Enum </tt>

The "Enum" module type is the following:

```ocaml
module type Enum = sig
  type t [@@deriving sexp_of, compare, enumerate]
end
```

The important part here is the `enumerate` derivation, which allows
Bonsai to list all possible combinations of that type.

#### <tt> ~which </tt>

The `which` function is defined by the user to translate cases from
the components input into values of their first-class module.

#### <tt> ~handle </tt>

This function translates the values returned by `~which` into
the Bonsai component that handles that kind of value.

### Examples

```ocaml
module Branch = struct
  type t =
    | Multiple_of_ten
    | Even
    | Odd
  [@@deriving sexp, compare, enumerate]
end
```

```ocaml
# let which input =
    if input mod 10 = 0
    then Branch.Multiple_of_ten
    else if input mod 2 = 0
    then Branch.Even
    else Branch.Odd
val which : int -> Branch.t = <fun>

# let handle = function
    | Branch.Multiple_of_ten -> Bonsai.pure ~f:(sprintf "%d is a multiple of ten")
    | Branch.Even-> Bonsai.pure ~f:(sprintf "%d is even")
    | Branch.Odd-> Bonsai.pure ~f:(sprintf "%d is odd")
val handle : Branch.t -> (int, string) Bonsai.t = <fun>

# let component = Bonsai.enum (module Branch) ~which ~handle
val component : (int, string) Bonsai.t = <fun>
```

## <tt> Bonsai.if_ </tt>

`Bonsai.if_` is a special case of `Bonsai.enum` with exactly two branches.

```ocaml
# Bonsai.if_
- : ('input -> bool) ->
    then_:('input, 'result) Bonsai.t ->
    else_:('input, 'result) Bonsai.t -> ('input, 'result) Bonsai.t
= <fun>
```

### Example

```ocaml
# let component =
    Bonsai.if_
      (fun input -> input % 2 = 0)
      ~then_:(Bonsai.pure ~f:(sprintf "%d is even"))
      ~else_:(Bonsai.pure ~f:(sprintf "%d is odd"))
val component : (int, string) Bonsai.t = <fun>
```
